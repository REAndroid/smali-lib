
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
    }
}

dependencies {
    implementation project(':util')
    api project(':dexlib2')
    implementation depends.jcommander

    testImplementation depends.junit
    testImplementation project(':smali')
    testImplementation depends.antlr_runtime
}

processResources.inputs.property('version', version)
processResources.expand('version': version)

// Build a separate jar that contains all dependencies
task fatJar(type: Jar) {
    from sourceSets.main.output
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }

    classifier = 'fat'

    manifest {
        attributes('Main-Class': 'org.jf.baksmali.Main')
    }

    doLast {
        if (!System.getProperty('os.name').toLowerCase().contains('windows')) {
            ant.symlink(link: file("${destinationDirectory.get()}/baksmali.jar"), resource: archivePath, overwrite: true)
        }
    }
}
tasks.getByPath('build').dependsOn(fatJar)


task fastbuild(dependsOn: build) {
}

task fb(dependsOn: fastbuild) {
}

tasks.getByPath('javadoc').onlyIf({
    !gradle.taskGraph.hasTask(fastbuild)
})

tasks.getByPath('test').onlyIf({
    !gradle.taskGraph.hasTask(fastbuild)
})